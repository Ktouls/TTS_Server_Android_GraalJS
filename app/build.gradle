plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
    id("kotlinx-serialization")
    id("kotlin-parcelize")
    id("com.google.devtools.ksp")
    id("com.mikepenz.aboutlibraries.plugin")
    alias(libs.plugins.compose.compiler)
}

static def buildTime() {
    def t = new Date().time / 1000
    return (long) t
}

static def releaseTime() {
    return new Date().format("yy.MMddHH", TimeZone.getTimeZone("GMT+8"))
}

def version = "1." + releaseTime()
def gitCommits = Integer.parseInt('git rev-list HEAD --count'.execute().text.trim())

android {
    compileSdk libs.versions.compileSdk.get().toInteger()
    namespace 'com.github.jing332.tts_server_android'

    defaultConfig {
        applicationId 'com.github.jing332.tts_server_android'
        minSdk libs.versions.minSdk.get().toInteger()
        targetSdk libs.versions.targetSdk.get().toInteger()
        versionCode gitCommits
        versionName version

        ksp {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
            arg("room.incremental", "true")
            arg("room.expandProjection", "true")
        }

        // è¯»å–strings.xmlæ‰€åœ¨æ–‡ä»¶å¤¹ è·å¾—åº”ç”¨æ”¯æŒçš„è¯­è¨€
        tasks.register('buildTranslationArray') {
            def defaultCode = "zh-CN"
            def foundLocales = new StringBuilder()
            foundLocales.append("new String[]{")

            fileTree("src/main/res").visit { details ->
                if (details.file.path.endsWith("strings.xml")) {
                    def path = details.file.parent.replaceAll('\\\\', "/")
                    def languageCode = path.tokenize('/').last().replaceAll('values-', '').replaceAll('-r', '-')
                    languageCode = (languageCode == "values") ? defaultCode : languageCode;
                    foundLocales.append("\"").append(languageCode).append("\"").append(",")
                }
            }
            foundLocales.append("}")
            def foundLocalesString = foundLocales.toString().replaceAll(',}', '}')
            buildConfigField "String[]", "TRANSLATION_ARRAY", foundLocalesString
        }
        preBuild.dependsOn buildTranslationArray
        buildConfigField "long", "BUILD_TIME", buildTime().toString()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    lint {
        checkReleaseBuilds false
        checkDependencies  true
    }

    signingConfigs {
        release {
            // read sign from local.properties
            Properties pro = new Properties()
            InputStream input = project.rootProject.file("local.properties").newDataInputStream()
            pro.load(input)
            storeFile file(pro.getProperty("KEY_PATH"))
            storePassword pro.getProperty("KEY_PASSWORD")
            keyAlias pro.getProperty("ALIAS_NAME")
            keyPassword pro.getProperty("ALIAS_PASSWORD")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'r8-rules.pro'
        }
        debug {
            signingConfig signingConfigs.release
            applicationIdSuffix ".debug"
            versionNameSuffix "_debug"
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/*']
        }
    }


    // åˆ†åˆ«æ‰“åŒ…APK åŸç‰ˆ å’Œ devå…±å­˜ç‰ˆ
    flavorDimensions += "version"
    productFlavors {
        app {
            dimension = "version"
        }
        dev {
            dimension = "version"
            applicationIdSuffix ".dev"
        }
    }

    android.applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            //noinspection GrDeprecatedAPIUsage
            def abiName = output.getFilter(com.android.build.OutputFile.ABI)
            if (abiName == null)
                output.outputFileName = "TTS-Server-v${variant.versionName}.apk"
            else
                output.outputFileName = "TTS-Server-v${variant.versionName}_${abiName}.apk"
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    kotlin {
        jvmToolchain(17)
    }

    buildFeatures {
        viewBinding true
        buildConfig true
        compose true
    }

}

dependencies {
    // WebDAV æ”¯æŒåº“
    implementation(libs.sardine)
    
    // æ‰©å±•å›¾æ ‡åº“
    implementation 'androidx.compose.material:material-icons-extended'

    coreLibraryDesugaring(libs.desugar)
    api(project(":lib-common"))
    implementation(project(":lib-compose"))
    implementation(project(":lib-script"))
    implementation(project(":lib-tts"))
    implementation(project(":lib-server"))


    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    implementation(libs.logging)
    implementation(libs.slf4j.api)
    
    // ğŸ› ï¸ å…³é”®ä¿®å¤ 1ï¼šåœ¨ Logback ä¾èµ–ä¸­å½»åº•æ’é™¤å¼•èµ·å†²çªçš„ XML åº“
    implementation(libs.logback.android) {
        exclude group: 'xmlpull', module: 'xmlpull'
        exclude group: 'xpp3', module: 'xpp3'
    }

    implementation libs.activityKtx
    implementation libs.bundles.andorid.base


    implementation libs.kotlinx.coroutines.android
    implementation libs.kotlinx.serialization.json

    implementation(libs.bundles.markwon)


    // Code Editor
    implementation(libs.bundles.codeEditor)
    implementation(libs.bundles.network)


    implementation(libs.bundles.room)
    ksp(libs.roomCompiler)


    implementation(libs.splitties.systemservices)
    implementation(libs.splitties.appctx)
    implementation(libs.hutool.crypto)


    implementation(libs.bundles.media3)

    implementation 'me.rosuh:AndroidFilePicker:1.0.1'

    implementation(libs.hanlp)


    implementation(libs.bundles.about)

    implementation libs.bundles.accompanist


    implementation(libs.bundles.lifecycle)

    def composeBom = platform(libs.compose.bom)
    implementation composeBom
    androidTestImplementation composeBom

    implementation(libs.bundles.compose)
    implementation(libs.bundles.compose.material3)

    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'

    testImplementation(libs.junit)
    androidTestImplementation(libs.ext.junit)
    androidTestImplementation(libs.espresso.core)
}

// ğŸ› ï¸ å…³é”®ä¿®å¤ 2ï¼šæ·»åŠ å…¨å±€æ’é™¤è§„åˆ™
// ç¡®ä¿æ²¡æœ‰ä»»ä½•é—´æ¥ä¾èµ–ï¼ˆå¦‚ sardine æˆ–å…¶ä»–ç½‘ç»œåº“ï¼‰å¸¦å…¥ xmlpull
configurations.all {
    exclude group: 'xmlpull', module: 'xmlpull'
    exclude group: 'xpp3', module: 'xpp3'
}
